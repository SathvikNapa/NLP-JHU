# This is a fragment of English grammar, somewhat imperfect in its
# description.  To understand the file format, first look through
# arith.gra, arith-infix.gra, and arith-typed.gra.  
#
# The grammar assumes that the input has been passed through some
# modest morphological preprocessing.  See the sample inputs in
# english.sen.
# 
# Some important omissions that you will not fix in this assignment:
# 
# - Slashed categories like S/NP, and their uses such as relative
#   clauses and questions.  The syntax and semantics of slashed
#   categories could indeed be handled in this framework, although it
#   is more elegant if one builds some special support into the formalism.
# 
# - Extra orders for quantifiers (e.g., the joke semantics for "A
#   woman has a baby every 15 minutes").  There is no attempt to
#   handle this.  It is actually tied up with the previous point.
#
# - The semantics for tense is not as sophisticated as what we did
#   in class.  Here I just write Past(love(Jill,Joe)) instead of
#     E%e past(e) ^ act(e,loving) ^ lover(e,Joe) ^ lovee(e,Jill)
#   This could be fixed (especially with some slashed category support),
#   but the semantics was already hard enough to read!  
#
# - There is no attempt to figure out what pronouns refer to.
#
# - Lots of interesting grammatical constructions are missing, like
#   passives.

# ----------------------------------------------------------------------

# This most basic rule of English combines a subject with a tensed VP
# (never a stem or infinitive VP).  The operators pres and past get
# wrapped around the semantics only here (see discussion of verb
# suffixes below).

1 S[=2 sem=pres(2(1))] NP[num=sing case=subj] VP[verb_agreement=sg tense=pres]
1 S[=2 sem=pres(2(1))] NP[num=mass case=subj] VP[verb_agreement=sg tense=pres]
1 S[=2 sem=pres(2(1))] NP[num=pl   case=subj] VP[verb_agreement=pl tense=pres]

1 V[=1 tense=pres verb_agreement=sg]  V[tense=stem] -s
1 V[=1 tense=pres verb_agreement=pl]  V[tense=stem] -0

1 S[=2 sem=2(1)]	NP[num=N]	VP[num=N tense=modal]

1 ROOT[sem="assert(1, speaker)" head=1]	S .
1 ROOT[sem="exclaim(1, speaker)" head=1]	S !
1 ROOT[sem="command(1(hearer), speaker)" head=1] VP[tense=stem] !  

1 VP[=1]		      V[arg=none]    # intransitive verbs: "sleep"
1 VP[=1 sem="1(2)"]   V[arg=np]   NP[case=obj]
1 VP[=1 tense=1 sem=1(2)]     V[arg=vpinf] VP[tense=inf]  # "want to eat"
1 VP[=1 tense=1 sem="1(2,3)"] V[arg=npvpinf] NP VP[tense=inf] # "want him to eat"
1 VP[=1 sem=1(2)]	      V[arg=cp] CP   # "say that ..."

1 VP[=1 verb_agreement=A sem=1(2)] V[arg=pred verb_agreement=A] Adj
1 VP[=1 verb_agreement=A sem=1(2)] V[arg=pred verb_agreement=A] N

1 CP[=2]		that S              # used for "say that ..."
1 NP[=2 sem=1(2) num=sing]  Det[req=sing  req_count=Y]   N[num=sing can_count=Y]
1 NP[=2 sem=1(2) num=pl]    Det[req=nonsing req_count=Y] N[num=pl   can_count=Y]
1 N[=2 sem="%x Base(x) ^ New(x)"]	Adj[sem=New] N[sem=Base]
1 PP[=1 sem="%x 1(2,x)"]	P NP
1 VP[=1 sem="%subj 2(1(subj))"]	VP PP
1 N[=1 sem="%x Base(x) ^ New(x)"]	N[sem=Base] PP[sem=New]


1 VP[=1 tense=1 head=2 sem="%subj perf(2(subj))"]  V[head=have] VP[tense=pastp]
1 VP[=1 tense=1 head=2 sem="%subj prog(2(subj))"]  V[head=be] VP[tense=presp]

1 VP[=1 tense=1 head=2 sem="%subj must(2(subj))"]  V[head=have] VP[tense=inf]           # has to eat
1 VP[=1 tense=1 head=2 sem="%subj will(2(subj))"]  V[head=go tense=presp] VP[tense=inf] # going to eat

1 VP[=2 tense=modal sem="%subj 1(2(subj))"]	   Modal VP[tense=stem]

1 Modal will
1 Modal would
1 Modal should
1 Modal could
1 Modal can
1 Modal might
1 Modal may
1 Modal must

1 V[=1 tense=past]	V[tense=stem] -ed
1 V[=1 tense=pastp]	V[tense=stem] -ed 
1 V[=1 tense=presp]	V[tense=stem] -ing
1 V[=1 tense=pres num=sing]	V[tense=stem] -s
1 V[=1 tense=pres num=pl]	V[tense=stem] -0   # -0 is an "empty morpheme"
1 V[=2 tense=inf]	to V[tense=stem]

1 V[=1 tense=pres verb_agreement=sg]  V[tense=stem] -s
1 V[=1 tense=pres verb_agreement=pl]  V[tense=stem] -0

1 V[head=have arg=np sem=possess tense=stem]		have
1 V[head=have arg=np sem=possess tense=past]		had
1 V[head=have arg=np sem=possess tense=pastp]		had
1 V[head=have arg=np sem=possess tense=presp]		having
1 V[head=have arg=np sem=possess tense=pres verb_agreement=sg]	has
1 V[head=have arg=np sem=possess tense=pres verb_agreement=pl]	have

1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=stem] 		be
1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=past verb_agreement=sg] 	was
1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=past verb_agreement=pl] 	were
1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=pastp] 		been
1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=presp] 		being
1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=pres verb_agreement=sg] 	is
1 V[head=be arg=pred sem="%pred %subj pred(subj)" tense=pres verb_agreement=pl] 	are

1 V[=1 arg=none tense=stem sem="%subj 1(subj)"] go    

1 V[=1 tense=stem arg=none sem="%subj 1(subj)"]		   sleep # intransitive
1 V[=1 tense=stem arg=np   sem="%obj %subj 1(obj,subj)"]   kiss  # transitive
1 V[=1 tense=stem arg=np   sem="%obj %subj 1(obj,subj)"]   love  # transitive
1 V[=1 tense=stem arg=np   sem="%obj %subj spoon(obj,subj)"] spoon # transitive ("spoon the custard")
1 V[=1 tense=stem arg=np   sem="%obj %subj 1(obj,subj)"]   eat   # transitive
1 V[=1 tense=stem arg=none sem="%subj 1(something,subj)"]  eat   # intransitive version
1 V[=1 tense=stem arg=np   sem="%obj %subj 1(obj,subj)"]   want # ...a pickle  
1 V[=1 tense=stem arg=vpinf sem="%pred %subj 1(pred(subj), subj)"]	 want # ...to sleep
1 V[=1 tense=stem arg=npvpinf sem="%obj %pred %subj 1(pred(obj), subj)"] want # ...Papa to sleep
1 V[=1 tense=stem arg=cp   sem="%prop %subj 1(prop, subj)"] say         # that ...
1 V[=1 tense=stem arg=cp   sem="%prop %subj 1(prop, subj)"] understand  # that ...
1 V[=1 tense=stem arg=np   sem="%obj %subj 1(obj,subj)"]   understand   # the president
1 V[=1 tense=stem arg=np   sem="%obj %subj 1(obj,subj)"]   entice   # transitive
1 V[=1 tense=stem arg=none sem="%subj 1(something,subj)"]  entice   # intransitive

1 NP[=1 num=sing] Papa
1 NP[=1 num=sing] Joe
1 NP[=1 num=sing] Jill
1 NP[=1 num=sing case=obj] her
1 NP[=1 num=sing case=subj sem=her] she        
1 NP[=1 num=sing case=obj] him
1 NP[=1 num=sing case=subj sem=him] he
1 NP[=1 num=sing case=subj] it
1 NP[=1 num=sing case=obj] it
1 NP[=1 num=pl case=subj] they
1 NP[=1 num=pl case=obj head=them sem=them] them

1 N[=1 num=pl] N[=1 num=sing] -s     # morphology

1 N[=1 num=sing can_count=Y] bonbon
1 N[=1 num=sing can_count=Y] spoon
1 N[=1 num=sing can_count=Y] president
1 N[=1 num=sing can_count=Y] pickle
1 N[=1 num=sing can_count=Y] sandwich
1 N[=1 num=sing can_count=Y] floor
1 N[=1 num=sing can_count=Y] man
1 N[=1 num=sing can_count=Y] woman
1 N[=1 num=sing sem=chief_of_staff can_count=Y] chief of staff
1 N[=1 num=pl sem=chief_of_staff can_count=Y] chiefs of staff
1 N[=1 num=pl   can_count=Y] N[num=sing can_count=Y] -s


# Determiners.

1 Det[=1 req=sing              sem=the]   the

1 Det[=1 req=sing  req_count=Y sem=some]  a
1 Det[=1 req=sing  req_count=Y sem=all]   every
1 Det[=1 req=sing  req_count=Y sem=all]   each
1 Det[=1 req=sing               sem=the]  the

1 Det[=1 req=nonsing req_count=N sem=all]   all
1 Det[=1 req=nonsing req_count=N sem=some_plural]   some
1 Det[=1 req=nonsing req_count=N sem=the_plural]    the

1 Det[=1 req=nonsing req_count=N sem="%n the(%x n(x)^possess(x,him))"]  his
1 Det[=1 req=nonsing req_count=N sem="%n the(%x n(x)^possess(x,her))"]  her
1 Det[=1 req=nonsing req_count=N sem="%n the(%x n(x)^possess(x,it))"]   its
1 Det[=1 req=nonsing req_count=N sem="%n the(%x n(x)^possess(x,them))"] their
1 Det[=1 req=nonsing req_count=Y sem=two]   two


1 Adj	fine
1 Adj	blue
1 Adj	delicious
1 Adj	perplexed
1 Adj	pickled
1 Adj[=2 sem=%x{1(2)}(x)]  very Adj   # as in {very(big)}(x); equivalent to very(big,x) but parentheses clarify it

# English has lots more prepositions besides these, of course.

1 P with
1 P on
1 P onto     
1 P under
1 P in

# Conjunction (with "and"; "or" is similar).  We don't allow "N and N"
# or "V and V" as these raise some complicated problems.  And we only
# allow VPs of the same tense to coordinate, so that the coordinated
# VP can inherit that tense (our kludgy treatment of suffix morphology
# makes this necessary).

1 Adj[=2 sem="%x 1(x)^3(x)"] Adj and Adj  # "fine and blue woman" has both properties
1 NP[=2 num=pl sem="and(1,3)"] NP and NP    
1 VP[=2 sem="%subj 1(subj) ^ 3(subj)"] VP[tense=0] and VP[tense=0]

# NP and N rules to accommodate mass nouns
1 NP[=2 sem=1(2) num=mass]  Det[req=nonsing req_count=N] N[num=mass can_count=N]
1 NP[=1 sem=the(1) num=mass]  N[num=mass can_count=N req_count=N]

1	N[=1 num=mass req_count=N can_count=N]	caviar
1	N[=1 num=mass req_count=N can_count=N]	sand
1   N[=1 num=mass req_count=N can_count=N]  rice
1	N[=1 num=mass req_count=N can_count=N]	time
1	N[=1 num=mass req_count=N can_count=N]	dna
